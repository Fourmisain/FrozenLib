plugins {
	id 'fabric-loom' version '0.13-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.7.+'
	id 'org.quiltmc.quilt-mappings-on-loom' version '4.2.1'
	id 'maven-publish'
}

def env = System.getenv()

archivesBaseName = project.archives_base_name
version = getVersion()
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/frozenblocklib.accesswidener")
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	maven {
		url = 'https://jitpack.io'
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"

		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url = 'https://maven.terraformersmc.com'

		content {
			includeGroup "com.terraformersmc"
		}
	}
	maven {
		url = 'https://maven.shedaniel.me/'
	}
	maven {
		url 'https://cursemaven.com'
		
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		name "Siphalor's Maven"
		url "https://maven.siphalor.de"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings(loom.layered {
		//addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${minecraft_version}+build.${quilt_mappings}:v2"))
		//mappings "net.fabricmc:yarn:1.19.2+build.4:v2"
		officialMojangMappings()
	})
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"
	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

	// Fabric-ASM
	modImplementation "com.github.Chocohead:Fabric-ASM:${fabric_asm_version}"
	include "com.github.Chocohead:Fabric-ASM:${fabric_asm_version}"

	//Simple Copper Pipes
	modImplementation "maven.modrinth:simple-copper-pipes:${copperpipes_version}"
	modImplementation "maven.modrinth:fastanim:${fastanim_version}"

	// Mod Menu
	modImplementation "com.terraformersmc:modmenu:${modmenu_version}"

	// NBT Crafting
	modImplementation "de.siphalor:nbtcrafting-1.19:2+"
	include "de.siphalor:nbtcrafting-1.19:2+"

	// only affects runClient, does not affect gradlew build. add -PuseThirdPartyMods=false to not use these
	if (findProperty("useThirdPartyMods") != "false") {
		modRuntimeOnly "maven.modrinth:ferrite-core:${ferritecore_version}"
		modRuntimeOnly "maven.modrinth:lazydfu:${lazydfu_version}"
		modRuntimeOnly "maven.modrinth:starlight:${starlight_version}"
		//modRuntimeOnly "maven.modrinth:indium:1.0.6+mc1.19"
		//modRuntimeOnly "maven.modrinth:lithium:mc1.19.1-0.8.3"
		//modRuntimeOnly "maven.modrinth:sodium:mc1.19-0.4.2"
		modRuntimeOnly "maven.modrinth:entityculling:${entityculling_version}"
		//modRuntimeOnly "maven.modrinth:c2me-fabric:0.2.0+alpha.8.32+1.19.1-rc3"
		modRuntimeOnly "maven.modrinth:ksyxis:${ksyxis_version}"
		//modRuntimeOnly "maven.modrinth:iris:1.19.x-v1.2.5"
		modRuntimeOnly "maven.modrinth:memoryleakfix:${memoryleakfix_version}"
		modRuntimeOnly "maven.modrinth:no-unused-chunks:${no_unused_chunks_version}"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
	options.fork = true
	options.incremental = true
}

tasks.withType(Test).configureEach {
	maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

String getVersion() {
	String version = "${mod_version}-${mod_loader}+${minecraft_version}"

	if (findProperty("releaseType") != "stable") {
		version += "-unstable"
	}

	return version
}

configurations {
	dev {
		canBeResolved = false
		canBeConsumed = true
	}
}

artifacts {
	dev jar
	archives remapJar
	archives sourcesJar
}

// configure the maven publication
publishing {
	publications {
		gpr(MavenPublication) {
			artifactId archivesBaseName
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/frozenblock/frozenlib")
			credentials {
				username = env.GITHUB_USER
				password = env.GITHUB_TOKEN
			}
		}
	}
}
