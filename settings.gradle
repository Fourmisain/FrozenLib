import java.nio.file.Files

pluginManagement {
    repositories {
        maven {
            name = 'Quilt'
            url = 'https://maven.quiltmc.org/repository/release/'
        }
		maven {
			name = 'Quilt Snapshot'
			url = 'https://maven.quiltmc.org/repository/snapshot/'
		}
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        jcenter()
        maven {
            name = "Forge"
            url = "https://files.minecraftforge.net/maven/"
        }
        maven {
            name = "Jitpack"
            url 'https://jitpack.io/'
        }
        mavenCentral()
        gradlePluginPortal()
    }
}

rootProject.name = "FrozenLib"

module("core")
module("datafixerupper")

def module(String module) {
	include(module)

	def moduleProject = project(":$module")
	moduleProject.projectDir = file("module/$module")

	rootProject.projectDir.toPath().resolve("module/$module/").toFile().listFiles().each {
		// Is the module disabled?
		if (it.isDirectory()
				&& it.name != "src" // Ignore sources
				&& it.name != "build" // Ignore build artifacts
				&& !it.name.startsWith(".") // Ignore anything hidden on unix-like OSes
		) {
			// Libraries can be disabled by adding a file named DISABLE at the root of its directory
			if (Files.exists(it.toPath().resolve("build.gradle")) && Files.notExists(it.toPath().resolve("DISABLE"))) {
				include "$module:$it.name"
			}
		}
	}
}
